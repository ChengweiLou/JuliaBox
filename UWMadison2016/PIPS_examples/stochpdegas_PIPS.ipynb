{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "push!(LOAD_PATH, pwd())\n",
    "using Ipopt\n",
    "using NetJuMP\n",
    "using JuMP\n",
    "import MPI\n",
    "include(\"NetParPipsNlp.jl\")\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "# Parameters\n",
    "TF=24*3600                           # horizon time - [s]\n",
    "Nt=24                                # number of temporal grid points\n",
    "Nx=3                                 # number of spatial grid points\n",
    "S=40                                  # number of scenarios\n",
    "\n",
    "TIMEG=1:Nt                           # set of temporal grid points\n",
    "TIMEGm=1:Nt-1                        # set of temporal grid points minus 1\n",
    "DIS=1:Nx                             # set of spatial grid points\n",
    "SCENG=1:S                            # scenario set\n",
    "\n",
    "# links\n",
    "type LinkData                        # set of links\n",
    "     name::ASCIIString\n",
    "     startloc::ASCIIString           # start node\n",
    "     endloc::ASCIIString             # end node\n",
    "     diam::Float64                   # link diameter - mm\n",
    "     length::Float64                 # link length - km\n",
    "     ltype::ASCIIString              # link type, passive or active\n",
    "     c1                              # aux constant\n",
    "     c2                              # aux constant\n",
    "     c3                              # aux constant\n",
    "     dx                              # spatial grid spacing - [m]\n",
    "     lam                             # friction coefficient - []\n",
    "     A                               # pipe transveral area - [m^2]\n",
    "end\n",
    "linkDict = Dict{ASCIIString, LinkData}()\n",
    "# nodes\n",
    "type NodeData\n",
    "     name::ASCIIString\n",
    "     pmin::Float64                    # min pessure - bar\n",
    "     pmax::Float64                    # max pressure - bar\n",
    "end\n",
    "nodeDict = Dict{ASCIIString, NodeData}()\n",
    "\n",
    "\n",
    "# supply\n",
    "type SupplyData                       # set of suppliers\n",
    "     name::ASCIIString\n",
    "     loc::ASCIIString                 # supply location\n",
    "     min::Float64                     # min supply - scmx106/day\n",
    "     max::Float64                     # max supply - scmx106/day\n",
    "end\n",
    "supDict = Dict{ASCIIString, SupplyData}()\n",
    "\n",
    "# demand\n",
    "type DemandData                        # set of suppliers\n",
    "     name::ASCIIString\n",
    "     loc::ASCIIString                  # demand location\n",
    "     d::Float64                        # base demand - scmx106/day\n",
    "     stochd                            # stochastic demands - [scmx10-4/hr]\n",
    "end\n",
    "demDict = Dict{ASCIIString, DemandData}()\n",
    "\n",
    "# physical data\n",
    "eps= 0.025                           # pipe rugosity - [mm]\n",
    "z= 0.80                              # gas compressibility  - []\n",
    "rhon=0.72                            # density of air at normal conditions - [kg/m3]\n",
    "R=8314.0                             # universal gas constant [J/kgmol-K]\n",
    "M=18.0                               # gas molar mass [kg/kgmol]\n",
    "pi=3.14                              # pi\n",
    "nu2=0                                # gas speed of sound [m2/s2]\n",
    "Tgas = 293.15                        # reference temperature [K]\n",
    "Cp = 2.34                            # heat capacity @ constant pressure [kJ/kg-K]\n",
    "Cv = 1.85                            # hat capacity @ constant volume [kJ/kg-K]\n",
    "gam = Cp/Cv                          # expansion coefficient [-]\n",
    "om = (gam-1.0)/gam                   # aux constant [-]\n",
    "U = 1.0*0.1                          # pipe heat transfer coefficient [J/m2-s-K]\n",
    "Tamb = 20+273.15                     # soil temperature [K]\n",
    "Tsup = 30+273.15                     # supply temperature [K]\n",
    "\n",
    "# scaling and constants\n",
    "ffac = 0                             # from scmx106/day to kg/s\n",
    "ffac2 = 0                            # from kg/s to scmx10-4/hr\n",
    "pfac = 0                             # from bar to Pa\n",
    "pfac2 = 0                            # from Pa to bar\n",
    "dfac = 0                             # from mm to m\n",
    "lfac = 0                             # from km to m\n",
    "c4 = 0                               # aux constant [kW/(scmx10-4/hr)]\n",
    "\n",
    "# cost factors\n",
    "ce = 0.1                             # cost of compression [$/kWh]\n",
    "cd = 1e6                             # demand tracking cost [-]\n",
    "cT = 1e6                             # terminal constraint cost [-]\n",
    "cs =   0                             # supply cost [$/scmx10-4]\n",
    "\n",
    "# define temporal discretization info\n",
    "dtG = 0                              # temporal grid spacing - [s]\n",
    "\n",
    "include(\"set_data_paper.jl\")\n",
    "NODE = keys(nodeDict)\n",
    "LINK = keys(linkDict)\n",
    "SUP =  keys(supDict)\n",
    "DEM =  keys(demDict)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "function createGasModel(SCEN)\n",
    "         m = Model(solver=IpoptSolver())\n",
    "         @variable(m, nodeDict[j].pmin<=p[SCEN, j in NODE, TIMEG]<=nodeDict[j].pmax, start= 50)             # node pressure - [bar]\n",
    "         #@variable(m, 0<=dp[SCEN, j = LINK,  TIMEG; linkDict[j].ltype == \"a\"]<=100, start= 10)             # compressor boost - [bar]\n",
    "         @variable(m, dp[SCEN, j = LINK,  TIMEG; linkDict[j].ltype == \"a\"], start= 10)                      # compressor boost - [bar]\n",
    "         @variable(m, 1<=fin[SCEN, LINK, TIMEG]<=500, start= 100)                                           # flow in pipe - [scmx10-4/hr]\n",
    "         @variable(m, 1<=fout[SCEN, LINK, TIMEG]<=500, start= 100)                                          # flow out pipe - [scmx10-4/hr]\n",
    "         @variable(m, 0.01<=sG[SCEN, j in SUP, TIMEG]<=supDict[j].max, start = 10)                          # supply flow - [scmx10-4/hr]\n",
    "         @variable(m, dem[SCEN, DEM, TIMEG],    start=100)                                                  # demand flow - [scmx10-4/hr]\n",
    "         @variable(m, 0<=pow[SCEN, j = LINK, TIMEG; linkDict[j].ltype == \"a\"]<=3000, start= 1000)           # compressor power - [kW]\n",
    "         @variable(m, slack[SCEN, LINK, TIMEG, DIS]>=0, start= 10)                                          # auxiliary variable\n",
    "\n",
    "         # define spatio-temporal variables\n",
    "         @variable(m, 10<=px[SCEN, LINK, TIMEG, DIS]<=100, start= 50)                                       # link pressure profile - [bar]\n",
    "         @variable(m, 1<=fx[SCEN, LINK, TIMEG, DIS]<=100, start= 100)                                       # link flow profile - [scmx10-4/hr]\n",
    "\n",
    "         # compressor equations\n",
    "         @addNLConstraint(m, powereq[i = SCEN, j = LINK, t = TIMEG; linkDict[j].ltype == \"a\"], pow[i,j,t] == c4*fin[i,j,t]*((px[i,j,t,1]/(px[i,j,t,1]-dp[i,j,t]))^om-1))\n",
    "\n",
    "         # node balance [mass]\n",
    "         @constraint(m,nodemeq[k = SCEN, i in NODE, t = TIMEG], sum{       fout[k,j,t], j in LINK ; linkDict[j].endloc==i}\n",
    "                                                                 - sum{       fin[k,j,t],  j in LINK ; linkDict[j].startloc==i}\n",
    "                                                                 + sum{        sG[k,j,t],  j in SUP ; supDict[j].loc == i }\n",
    "                                                                 - sum{       dem[k,j,t],  j in DEM ; demDict[j].loc == i }\n",
    "                                                                 ==0)\n",
    "\n",
    "         # flow equations for passive and active links\n",
    "         @constraint(m, flow[i = SCEN, j = LINK, t = TIMEGm, k = 1:(Nx-1)], (px[i,j,t+1,k]-px[i,j,t,k])/dtG + linkDict[j].c1*(fx[i,j,t+1,k+1]-fx[i,j,t+1,k])/(linkDict[j].dx)==0)\n",
    "\n",
    "         # boundary conditions flow\n",
    "         @constraint(m, flow_start[i = SCEN, j = LINK, t = TIMEG], fx[i,j,t,1]==fin[i,j,t])\n",
    "         @constraint(m, flow_end[i = SCEN, j = LINK, t = TIMEG], fx[i,j,t,Nx]==fout[i,j,t])\n",
    "\n",
    "         # pressure equations for passive and active links\n",
    "         @constraint(m, press[i = SCEN, j = LINK, t = TIMEGm,k = 1:(Nx-1)], (fx[i,j,t+1,k]-fx[i,j,t,k])/dtG == - linkDict[j].c2*(px[i,j,t+1,k+1]-px[i,j,t+1,k])/linkDict[j].dx - slack[i,j,t+1,k])\n",
    "         @addNLConstraint(m, slackeq[i = SCEN, j = LINK, t = TIMEG, k = 1:Nx],  slack[i,j,t,k]*px[i,j,t,k] - linkDict[j].c3*fx[i,j,t,k]*fx[i,j,t,k] == 0);\n",
    "\n",
    "         # boundary conditions pressure, passive links\n",
    "         @constraint(m, presspas_start[i = SCEN, j = LINK, t = TIMEG; linkDict[j].ltype == \"p\"], px[i,j,t,1] ==  p[i,linkDict[j].startloc,t])\n",
    "         @constraint(m,   presspas_end[i = SCEN, j = LINK, t = TIMEG; linkDict[j].ltype == \"p\"], px[i,j,t,Nx] == p[i,linkDict[j].endloc,t])\n",
    "\n",
    "         # boundary conditions, active links\n",
    "         @constraint(m, pressact_start[i = SCEN, j = LINK, t = TIMEG; linkDict[j].ltype == \"a\"], px[i,j,t,1] ==  p[i,linkDict[j].startloc,t] + dp[i,j,t])\n",
    "         @constraint(m,   pressact_end[i = SCEN, j = LINK, t = TIMEG; linkDict[j].ltype == \"a\"], px[i,j,t,Nx] == p[i,linkDict[j].endloc,t])\n",
    "\n",
    "         # fix pressure at supply nodes\n",
    "         @constraint(m, suppres[i = SCEN, j in SUP, t = TIMEG], p[i,supDict[j].loc,t] == nodeDict[supDict[j].loc].pmin)\n",
    "\n",
    "         # discharge pressure for compressors\n",
    "         @constraint(m, dispress[i in SCEN,j in LINK,t in TIMEG; linkDict[j].ltype==\"a\"],  p[i,linkDict[j].startloc,t]+dp[i,j,t]<=nodeDict[linkDict[j].startloc].pmax)\n",
    "        # line pack constraints\n",
    "         @constraint(m, line_packT[i in SCEN],  sum{  sum{fx[i,j,Nt,k], k in DIS}*linkDict[j].dx, j in LINK} >= sum{ sum{fx[i,j,1,k],k in DIS}*linkDict[j].dx, j = LINK})\n",
    "\n",
    "         # ss constraints\n",
    "         @constraint(m, flow_ss[i = SCEN, j = LINK, t =0, k = 1:(Nx-1)], (fx[i,j,t+1,k+1]-fx[i,j,t+1,k])==0)\n",
    "         @constraint(m, pres_ss[i = SCEN, j = LINK, t =0, k = 1:(Nx-1)],  - linkDict[j].c2*(px[i,j, t+1,k+1]-px[i,j,t+1,k])/linkDict[j].dx - slack[i,j,t+1,k] == 0)\n",
    "\n",
    "         @setObjective(m, Min, 1e-6*(1.0/S)*sum{ce*pow[i,j,t]*(dtG/3600),i= SCEN, j = LINK,t = TIMEG; linkDict[j].ltype == \"a\"}\n",
    "                              +1e-6*(1.0/S)*sum{cd*(dem[i,j,t]-demDict[j].stochd[i,t])^2, i in SCEN, j in DEM, t in TIMEG})\n",
    "\n",
    "\n",
    "         # non-anticipativity constraints (note this is only for t==1)\n",
    "         if (length(SCEN)> 1)\n",
    "             bs = SCEN[1]\n",
    "             @constraint(m,  nonantdq[i in SCEN,j in LINK,t in TIMEG; linkDict[j].ltype ==\"a\" && t ==1 && i != bs],   dp[i,j,t] ==  dp[bs,j,t])\n",
    "             @constraint(m,  nonantde[i in SCEN,j in DEM, t in TIMEG;                            t ==1 && i != bs],   dem[i,j,t]== dem[bs,j,t])\n",
    "         end\n",
    "\n",
    "         return m\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "# build the model and solve with IPOPT\n",
    "IL=createGasModel(SCENG)\n",
    "solve(IL)\n",
    "println(\"obj \", getObjectiveValue(IL))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "# build with PLASMO and solve with PIPS-NLP\n",
    "IL = NetModel()\n",
    "@variable(IL, dp[j = LINK; linkDict[j].ltype == \"a\"], start= 10)                       # compressor boost - [bar]\n",
    "@variable(IL, dem[DEM],    start=100)                                                  # demand flow - [scmx10-4/hr]\n",
    "for s in SCENG\n",
    "   single_scenario = createGasModel(s:s)\n",
    "   @addNode(IL, single_scenario, \"s$s\")\n",
    "   @constraint(IL,  nonantdq[j in LINK,t in TIMEG; linkDict[j].ltype ==\"a\" && t ==1],   dp[j] ==  getVar(single_scenario, :dp)[s,j,t])\n",
    "   @constraint(IL,  nonantde[j in DEM, t in TIMEG;                            t ==1],   dem[j]==  getVar(single_scenario,:dem)[s,j,t])\n",
    "end\n",
    "ParPipsNlp_solve(IL)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.4.2",
   "language": "julia",
   "name": "julia-0.4"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.4.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
